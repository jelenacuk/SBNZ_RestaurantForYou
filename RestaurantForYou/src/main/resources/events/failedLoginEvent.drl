package events;
import com.sbnz.RestaurantForYou.events.FailedLogInEvent;
import com.sbnz.RestaurantForYou.model.*;


declare SuspiciousUserEvent
    @role(event)
    @expires(3m)
    user: User
    reason: String
end;

rule "More than 10 logging in unsuccessfully in 1 minute from one user"
    when
        $t1: FailedLogInEvent($id: user.getId(), $user: user)
        Number(intValue >= 10) from accumulate(
            $t2: FailedLogInEvent(
                this != $t1, 
                user.getId() == $id) over window:time( 1m ),
            count($t2)
        )
        not (SuspiciousUserEvent(user.getId() == $id, 
        	reason == "More than 10 unsuccessful login attempts in 2 minutes from one user."))
    then
    	modify($user) { setActive(false) }
    	String reason = "More than 10 unsuccessful login attempts in 2 minutes from one user.";
        insert(new SuspiciousUserEvent($user, reason));
end;